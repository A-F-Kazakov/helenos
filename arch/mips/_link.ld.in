/*
 *  MIPS linker script
 *  
 *  kernel text
 *  kernel data
 *  
 */
#undef mips
#define mips mips

OUTPUT_FORMAT(BFD)

OUTPUT_ARCH(mips)

ENTRY(kernel_image_start) 

SECTIONS {
	. = 0x80100000;
	.text : {
		ktext_start = .;
		*(.text);
		ktext_end = .;
	}
	.data : {
		kdata_start = .;
		*(.data);		/* initialized data */
		hardcoded_ktext_size = .;
		LONG(ktext_end - ktext_start);	
		hardcoded_kdata_size = .;
		LONG(kdata_end - kdata_start);
		hardcoded_load_address = .;
		LONG(0x80100000);
	}
	_gp = . + 0x8000;
	.lit8 : { *(.lit8) }
	.lit4 : { *(.lit4) }
	.sdata : { *(.sdata) }
	.sbss : {
		*(.sbss);
		*(.scommon);
	}
	.bss : {
		*(.bss);		/* uninitialized static variables */
		*(COMMON); 		/* global variables */
	}
	mysymtable : {
		symbol_table = .;
		*(symtab.*);            /* Symbol table, must be LAST symbol!*/
	}
	kdata_end = .;

	.mdebug : { *(.mdebug) }
	.stab 0 : { *(.stab) }
	.stabstr 0 : { *(.stabstr) }
	.comment : { *(.comment) }
	.note : { *(.note) }
}
